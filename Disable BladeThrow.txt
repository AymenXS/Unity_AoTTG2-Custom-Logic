class Main
{   
    Description = "Survive a single round of titans.";
    Titans = 15;
    _hasSpawned = false;
    _myCharacter = null;

    function OnGameStart()
    {
        if (Network.IsMasterClient)
        {
            Game.SpawnTitans("Default", self.Titans);
        }
    }
    function OnCharacterSpawn(character)
    {
        if (character.IsMine && character.Type == "Human")
        {
            self._myCharacter = character;
        }
    }

    function OnTick()
    {
        if (Network.IsMasterClient && !Game.IsEnding)
        {
            titans = Game.Titans.Count;
            humans = Game.Humans.Count;
            playerShifters = Game.PlayerShifters.Count;
            if (humans > 0 || playerShifters > 0)
            {
                self._hasSpawned = true;
            }
            if (titans == 0)
            {
                UI.SetLabelAll("MiddleCenter", "Humanity wins!");
                Game.End(10.0);
                return;
            }
            if (humans == 0 && playerShifters == 0 && self._hasSpawned)
            {
                UI.SetLabelAll("MiddleCenter", "Humanity failed!");
                Game.End(10.0);
                return;
            }
            
            UI.SetLabelAll("TopCenter", "Titans Left: " + Convert.ToString(titans));
        }
        if (self._myCharacter != null && self._myCharacter.CurrentSpecial == "BladeThrow")
        {
            self._myCharacter.SetSpecial("Smell");
        }    
    }
}